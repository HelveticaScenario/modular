// Pest grammar for the Musical DSL pattern language.
// Mirrors `src/dsl/mini.ohm`.

Program = { SOI ~ Element* ~ Modifier* ~ EOI }

Element = { RandomChoice | NonRandomElement }

NonRandomElement = { FastSubsequence | SlowSubsequence | Value }

FastSubsequence = { "[" ~ Element* ~ "]" }
SlowSubsequence = { "<" ~ Element* ~ ">" }

RandomChoice = { NonRandomElement ~ ("|" ~ NonRandomElement)+ }

// Value types - order matters for precedence
// VoltsValue must come before NumericLiteral to match "1v" before "1"
// Interpolation must come early to match placeholder characters used for template strings
Value = { Rest | Interpolation | HzValue | VoltsValue | NoteName | NumericLiteral | ModuleRef }

Rest = { "~" }

// Interpolation placeholder - uses NUL character (ASCII 0x00 / 0)
// This character is used to mask ${...} interpolations in template strings
// during parsing to preserve correct span positions
Interpolation = { "\x00"+ }

ModuleRef = { "module(" ~ ModuleId ~ ":" ~ PortName ~ ")" }
ModuleId = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }
PortName = @{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

// Explicit volts suffix
VoltsValue = { SignedDecimal ~ VoltsSuffix }
VoltsSuffix = @{ "v" | "V" }

HzValue = { SignedDecimal ~ HzSuffix }
HzSuffix = @{ (("h" | "H") ~ ("z" | "Z")) | (("k" | "K") ~ ("h" | "H") ~ ("z" | "Z")) }

NumericLiteral = { SignedDecimal }
SignedDecimal = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

NoteName = @{ NoteLetter ~ Accidental? ~ Octave? }
NoteLetter = { "a" | "b" | "c" | "d" | "e" | "f" | "g" | "A" | "B" | "C" | "D" | "E" | "F" | "G" }
Accidental = { "#" | "b" }
Octave = @{ "-"? ~ ASCII_DIGIT+ }

// Modifiers section - multiple modifiers allowed
Modifier = { ScaleModifier | AddModifier }

// Scale modifier can now contain a pattern of scales
ScaleModifier = { "$" ~ "scale" ~ "(" ~ ScalePattern ~ ")" }
ScalePattern = { ScalePatternSequence | ScaleDefinition }
ScalePatternSequence = { ScaleFastSubsequence | ScaleSlowSubsequence }
ScaleFastSubsequence = { "[" ~ ScalePatternElement* ~ "]" }
ScaleSlowSubsequence = { "<" ~ ScalePatternElement* ~ ">" }
ScalePatternElement = { ScaleRandomChoice | ScalePatternSequence | ScaleDefinition }
ScaleRandomChoice = { (ScalePatternSequence | ScaleDefinition) ~ ("|" ~ (ScalePatternSequence | ScaleDefinition))+ }
ScaleDefinition = { NoteName ~ ":" ~ ScaleName }
ScaleName = @{ ASCII_ALPHA+ }

// Add modifier - contains a pattern of values to add
AddModifier = { "$" ~ "add" ~ "(" ~ Element+ ~ ")" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
